apply from: rootProject.file('base-client.gradle')

dependencies {
    implementation project(':support:verification-code')
    implementation project(':support:auth')
    implementation project(':support:common')

}

version(getRootProject().getVersion())

ext{
     artifactId = 'server-main'
}

bootJar {
    // 修改输出文件名，增加构建号
    def version = archiveVersion.get() + '-' + getBuildNumber()
    archiveFileName = "${archiveBaseName.get()}-${version}.${archiveExtension.get()}"
}

jib {
    from {
        image 'adoptopenjdk/openjdk11:x86_64-ubuntu-jdk-11.0.9.1_1'
    }
    to {
        image 'docker.pkg.github.com/lize3420/my-server/my-server-main'
        auth {
            username System.getenv("GITHUB_ACTOR") ?: "lize3420"
            password System.getenv("GITHUB_TOKEN") ?: "password"
        }
    }
}

// maven publish
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}
publishing {

    repositories {
        maven {
            name = "GitHubPackages"
            url = "http://maven.pkg.github.com/lize3420/my-server"
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: "lize3420"
                password = System.getenv("GITHUB_TOKEN") ?: "password"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = artifactId
            version = version + '-' + getBuildNumber()

            from components.java
        }
    }
}

def getBuildNumber(){
    return System.getenv("GITHUB_RUN_NUMBER") ?: '1'
}